*
* PURPOSE:  To test the SAC graphics environment and graphics action commands.
*
transcript create file $2$graphics.trans content all
wait text off
if $1$ eq sgf
  sgf directory $2$
endif
begindevice $1$

echo off
message ' '
message 'The next set of commands reads in a single data file and plots it.'
message ' '
echo on
cut a -.5 2.0
read $3$cdv_z
title 'A sample seismogram from a local event'
xlabel 'Time @(seconds@)'
ylabel 'Velocity @(nm/sec@)'
plot
listhdr columns 2
if $1$ ne sgf
  pause
endif
cut off
message ''
message 'The next set of commands reads in three files containing'
message 'amplitude responses from three stations and produces an'
message 'overlay plot in LINLOG format.'
message ''
echo on
read $3$*_z_am
xlim .04 .16
ylim .0001 .006
linlog
title 'Rayleigh Wave Amplitude Spectra for NESSEL'
xlabel 'Frequency @(Hz@)'
ylabel 'Amplitude'
symbol 2 increment
window 2 x .01 .5 y .45 .9
beginwindow 2
plot2
if $1$ ne sgf
  pause
endif
echo off
symbol off
title off
xlabel off
ylabel off
xlim off
ylim off
linlin
echo off
message ''
message 'The next set of commands reads part of a signal around the'
message 'first arrival time, takes its FFT, and then plots the signal'
message 'and its amplitude response in both LINLOG and LOGLOG format.'
message ''
echo on
cut a -0.1 n 512
read $3$cdv_z
taper
dft womean amph
writesp $3$temp
read $3$cdv_z
title 'SEISMIC TRACE'
fileid off
window 3 x .04 .53 y .48 .93
bw 3
begfr
xwind .1 .9
ywind .7 .9
qdp off
linlin
plot
ywind .15 .55
xwind .1 .45
linlog
cut off
read $3$temp.am
title 'Amplitude Response [linlog]'
ylim 1e-5 1
plot
xwind .55 .9
loglog
title 'Amplitude Response [loglog]'
xlim 1 60
plot
endfr
if $1$ ne sgf
  pause
endif
echo off
title off
xlim off
linlin
ylim off
qdp on
xwind .1 .9
ywind .15 .9
echo off
message ''
message 'The next set of commands reads a data file of type xyz'
message 'and first contours the data in quick scan mode'
message 'and then in a slower mode with label contours and linestyles'
message ''
echo on
read $3$contourdata
listhdr columns 2
zlevels increment 25 range 600 1400
title 'Test of contouring features [quick scan mode]'
contour
zlabels on list int off off off
zlines on list 1 2 2 2
title 'Test of contouring features [labels and linestyles]'
contour
transcript close file $2$graphics.trans
